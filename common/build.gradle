apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: 'co.touchlab.kotlinxcodesync'
apply plugin: 'com.squareup.sqldelight'

kotlin {
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation "com.soywiz.korlibs.klock:klock:$klock_version"
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
                implementation "com.squareup.sqldelight:runtime:$sqlDelight_version"
                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "io.ktor:ktor-client-serialization:$ktor_version"
                implementation "io.ktor:ktor-client-logging:$ktor_version"
                api "io.xorum:ReKamp:$reKamp_version"
            }
        }

        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
            }
        }

        iOSMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"
                implementation "com.squareup.sqldelight:native-driver:$sqlDelight_version"
                implementation "io.ktor:ktor-client-ios:$ktor_version"
                implementation "io.ktor:ktor-client-serialization-native:$ktor_version"
                implementation "io.ktor:ktor-client-logging-native:$ktor_version"
            }
        }

        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                implementation "com.squareup.sqldelight:sqlite-driver:$sqlDelight_version"
                implementation "com.squareup.sqldelight:runtime-jvm:$sqlDelight_version"
                implementation "io.ktor:ktor-client-serialization-jvm:$ktor_version"
                api "io.ktor:ktor-client-logging-jvm:$ktor_version"
                api "io.xorum:ReKamp-jvm:$reKamp_version"
            }
        }

        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
                implementation "junit:junit:4.13"
            }
        }
    }

    targets {
        def buildForDevice = project.findProperty("kotlin.native.cocoapods.target") == "ios_arm"
        if (buildForDevice) {
            iosArm64("iOS64") {
                compilations.main.extraOpts '-Xobjc-generics'
            }
            iosArm32("iOS32") {
                compilations.main.extraOpts '-Xobjc-generics'
            }

            sourceSets["iOS64Main"].dependsOn(sourceSets.iOSMain)
            sourceSets["iOS64Main"].dependencies { api "io.xorum:ReKamp-iosarm64:$reKamp_version" }

            sourceSets["iOS32Main"].dependsOn(sourceSets.iOSMain)
            sourceSets["iOS32Main"].dependencies { api "io.xorum:ReKamp-iosarm32:$reKamp_version" }
        } else {
            iosX64('iosX') {
                compilations.main.extraOpts '-Xobjc-generics'
            }

            sourceSets["iosXMain"].dependsOn(sourceSets.iOSMain)
            sourceSets["iosXMain"].dependencies { api "io.xorum:ReKamp-iosx64:$reKamp_version" }
        }
    }

    cocoapods {
        summary = "Codeforces WatchR common module"
        homepage = "https://github.com/xorum-io/codeforces_watcher"
    }
}

configurations {
    compileClasspath
}

xcode {
    projectPath = "../ios/Codeforces Watcher.xcodeproj"
    target = "Codeforces Watcher"
}

sqldelight {
    CWDatabase {
        packageName = "io.xorum.codeforceswatcher"
        sourceFolders = ["sqldelight"]
    }
}
